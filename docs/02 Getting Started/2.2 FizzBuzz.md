<iframe src="/.ibox.html?raw=true" style="border:none; position:fixed; width:40px; right:0; z-index=999;"></iframe>

# FizzBuzz
---

Let's dive into using the ogma REPL by programming the FizzBuzz game!
FizzBuzz is a simple word game which is used as a simple programming test. Here is a
description of the task:

> Write a program that prints the numbers from 1 to 100.
> But for multiples of three print “Fizz” instead of the number and for the 
> multiples of five print “Buzz”. For numbers which are multiples of both three
> and five print “FizzBuzz”.

To achieve this using ogma, the use of `range`, `map`, `mod` and string concatenation is
demonstrated.
The FizzBuzz problem can be broken down into the following 'steps':

1. Create a table ranging from 1 to 100,
2. Use `map` to convert `i` into a string,
3. If `i` / 3 is 0, write `Fizz`, else pass through empty string,
4. Concatenate `Buzz` if `i` / 5 is 0, otherwise concatenate empty string,
5. If result is empty string, convert `i` to string using `to-str`, otherwise pass through
   result.

Enter the following into the REPL:
```plaintext
range 1 101 | map i { let $e |
    if { \$e | mod 3 | = 0 } 'Fizz' '' |
    + if { \$e | mod 5 | = 0 } 'Buzz' '' |
    if {empty} { \$e | to-str } #i
} 
```

**Let's break this down.**

Firstly, ogma is built around the idea of **_pipes_**, as in, _piping_ data from one command to the
next. The first 'block' is everything up to the pipe (`|`) character: `range 1 101`.
This simply outputs a table with _header_ `i`, and numbers 1 to 100. You can get the output of this
command by simplying entering `range 1 101` into the REPL.

> ℹ️ You can gleam more information about the syntax and semantics of a command by using 
> `<cmd> --help` in the REPL.
> Try it out with `range --help`!

The output of the `range` command is a `Table`. `Table`'s are first class types in ogma, with many
commands providing efficient ways to work with them. This makes `Table`s one of the most common
data types and it is worth thinking about your data as being represented by a table.

We then run the `map` command _over the output of the previous block_. `map` takes a column and
_replaces_ the value in each row with the result of some _expression_.
Everything inside the curly braces is the expression!

**It is important to note the the _input_ into the expression block is the value found _in each
row_.** `map` works by iterating through each row, extracting the value that is in the given
column, feeding this value into the expression, and then replacing the value with the result of the
expression.

So let's see how this works:
```plaintext
Input is Num --> { let $e | <-- Assigns number to variable e
    if { \$e | mod 3 | = 0 } 'Fizz' '' | <-- If e is divisible by 3, return Fizz, else empty string
    + if { \$e | mod 5 | = 0 } 'Buzz' '' | <-- If e is divisible by 5, return Buzz, else empty string
    if {empty} { \$e | to-str } #i <-- If empty, return the number (as a string), else return string
} --> Output of expression is a Str
```

The steps that take place here are:
1. Assign the value in the row to the variable `$e`,
2. If `$e` is divisible by 3, return `Fizz`,
3. If `$e` is divisible by 5, return `Buzz`,
4. Concatenate `Fizz` and `Buzz` together,
5. Return the number as a string if the return is empty, else `Fizz`, `Buzz`, or `FizzBuzz`.

Try looking at the help of `if`: `if --help`. You will see that `if` requires expressions for
_both_ branches. This is because each command _must have an output type_. ogma is strongly
typed, so the output types of both branches must match!

Another command that is used is the string concatenation: `+`. The syntax might seem a bit
unwieldy at first; ogma uses _prefix notation_.

## Refactoring
The code right now is concise but somewhat hard to read. It would be nice to separate out some of
the logic so that it can be read a bit more easily.
This will introduce _defs_, the ability to specify a _user definition_.

Let's first create a definition which does the modulos:
```plaintext
def if-div Num (div:Num yes:Str) { mod $div | if {= 0} $yes '' }
```

This definition is conceptually much more simple: if the _input_ modulo `$div` is equal to zero,
return the `$yes` variable, else return an empty string.
We have annotated the types here (which is optional but does provide better error checking), saying
that the _input_ must be of type `Num`, the `$div` variable as the first parameter must be of type
`Num`, and the `$yes` variable as the second parameter must be of type `Str`. Since the `if`
command requires that the two branches must have matching types, this makes this definition type
check.

> ℹ️ Using the REPL allows one to quickly checkout if the definition works!
> Try inputting the following and seeing the output:
> ```plaintext
> \ 1 | if-div 3 'Fizz'
> \ 15 | if-div 3 'Fizz'
> \ 15 | if-divv 5 'Buzz'
> ```

Now that we have a `if-div` definition, let's also create a definition which takes a number and
applies the FizzBuzz logic:
```plaintext
def to-fizz-buzz Num () {
    let {if-div 3 'Fizz'} $f {if-div 5 'Buzz'} $b
    | \'' | + $f $b
}
```

This definition utilises the `if-div` and overloads the `let` command. It assigns the Fizz results
to `$f`, and the Buzz result to `$b`, and then it does something special: it alters the current
pipeline's _input_! The `\` command is colloquially known as the 'input' command since it will set
the input of the next block as the result of the first parameter. This is useful when
needing to get the _type_ of the input into something else.

We can now rewrite the original expression to make use of `to-fizz-buzz`:
```plaintext
range 1 101 | map i { let {to-str} $n | to-fizz-buzz | if {empty} $n #i }
```

This example more clearly demonstrates the use of pipes. The map expression stringifies the input
number and assigns it to `$n`, storing it for later. Then, `to-fizz-buzz` is invoked on the input
number, finally, the empty test is applied, returning the result of `to-fizz-buzz` or the
stringified input number.

## Concluding Thoughts
This worked example has demonstrated two ways to implement the FizzBuzz game in ogma. If you have
managed to follow along, congratulations! ogma may feel unfamiliar at first, and that is okay! The
use of pipelines is rare, users with experience of `bash` might be more comfortable with it. The
functional style of ogma may also be unfamiliar.
Ultimately we encourage you to continue experimenting with the REPL and continue working through
the documentation to understand more of ogma.
If you need some inspiration as to what ogma can achieve, please refer to the
[Examples](../15%20Examples?book=true) section. Another good source are the videos posted to the
[daedalus Youtube channel](https://www.youtube.com/channel/UCIjpIZpkoymWThuqqbOKbtA).
