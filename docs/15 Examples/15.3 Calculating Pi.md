<iframe src="../.ibox.html?raw=true" style="border:none; position:fixed; width:40px; right:0; z-index=999;"></iframe>

# Calculating Pi using a Monte Carlo simulation

Pi can be estimated using a Monte Carlo simulation and some algebra. The set up is to enclose a
circle of radius 1 inside a square (which side lengths will be `2r = 2`). What proportion of the
square's area is covered by the circle? Using the area of a square (`s * s`) and area of a circle
(`pi * r * r`) the proportion is: `pi * r * r / (2r * 2r) = pi / 4`. So to estimate Pi, a
simulation can be run which _randomly_ samples a point within the square's bounds, and check
whether this point is _also inside the circle bounds_. With enough samples, the ratio of points
_inside_ to _outside_ will be roughly equal to `pi / 4`, at which point multiplying the ratio by 4
gives one Pi.

To check if a point is inside the circle, it is simple to calculate magnitude of the vector of the
point and check if it is less than or equal to 1. This works as the radius of the circle is 1 to
begin with.

Below is the code with comments for each definition. Whilst it is possible to write a single block
which does the whole algorithm, it is good style to break it down into smaller, more understandable
chunks. This example is demonstrated in a [video on Youtube](https://youtu.be/sFun7N9OpiQ).

<iframe width="560" height="315" src="https://www.youtube.com/embed/sFun7N9OpiQ" title="YouTube video player" frameborder="0" allow="accelerometer; autoplay; clipboard-write; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>

## Expression

---

```plaintext
# Monte Carlo Pi estimation

# Define a structure which contains the sampled point.
def-ty Sample { x:Num y:Num }

# Randomly sample a point in the square space of (-1,1).
def sample () { Sample {rand -1 1} {rand -1 1} }

# Square the input by multiplying it by itself.
def sq Num () { * #i }

# The magnitude of an x,y point is the square root of the sum of its squares.
def mag Sample () { + {get x | sq} {get y | sq} | root 2 }

# The sample is considered inside the circle if its magnitude is <= 1.
# Handle a zero-length case where each component is zero.
def is-inside-circle Sample () { if {and {get x|=0} {get y|=0}} #t {mag | <= 1} }

# Build a table of samples and flag if inside or not of circle.
# Not strictly necessary but is a good way of building up to the solution
# and checking everthing works.
def build-samples Num () { range 0 #i |
    append --'sample point' sample |
    append --'inside' { get --Sample 'sample point' | is-inside-circle }
}

# Estimate Pi by counting the occurrences of samples inside the circle
# and ratioing them against the number of samples taken (the table length).
# Don't forget to multiple this ratio by 4!
def est-pi Table () { let $t |
    fold 0 { + { \$row | get --Bool 'inside' | if #i 1 0 } } |
    / { \$t | len } | * 4
}
```

Test it out!

![](../assets/examples.pi.png?raw=true)
