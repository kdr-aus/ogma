<iframe src="../.ibox.html?raw=true" style="border:none; position:fixed; width:40px; right:0; z-index=999;"></iframe>

# 3D Vector

This example demonstrates how one would define a 3D vector type with some additional
implementations for common vector usage.

First up; define the data structure. Simply define a 3 field type, all with numbers:

```ogma
def-ty Vec { x:Num y:Num z:Num }
```

Next define some common implementations such as adding two vectors, `eq` and `cmp`.

```ogma
def + Vec (rhs:Vec) { Vec
    {get x|+ $rhs.x}
    {get y|+ $rhs.y}
    {get z|+ $rhs.z}
}

def eq Vec (rhs:Vec) { and {get x|=$rhs.x} {get y|=$rhs.y} {get z|=$rhs.z} }

def cmp Vec (rhs:Vec) { let {get x|cmp $rhs.x} $x {get y|cmp $rhs.y} $y {get z|cmp $rhs.z} $z |
    if {\$x | != Ord::Eq} $x
       {\$y | != Ord::Eq} $y
       $z
}
```

It is a good idea to test as you go:

```ogma
>> Vec 1 2 3 | + Vec 5 6 7
Vec (x = 6, y = 8, z = 10)
>> Vec 1 2 3 | = Vec 2 3 1
false
>> Vec 1 2 3 | cmp Vec 1 2 4
Lt
>> Vec 1 2 3 | cmp Vec 1 2 3
Eq
```

Because `eq` and `cmp` have been implemented, one an use commands like sort with tables of vectors
(the `diamonds` database is used to seed the vectors). **Notice the use of `sort-by` rather than
`sort`**.

![](../assets/examples.vec.png?raw=true)

Let's implement unit vector, dot product and cross product:

```ogma
>> def unit Vec () { let $v | \0 |
    + {\$v.x|*$v.x} {\$v.y|*$v.y} {\$v.z|*$v.z} |
    root 2 | let $m |
    Vec {\$v.x|/$m} {\$v.y|/$m} {\$v.z|/$m}
}

>> Vec 5 10 3 | unit
Vec (
    x = 0.43193421279068006
    y = 0.863868425581360
    z = 0.25916052767440806
)

>> def dot Vec (rhs:Vec) {
    let {get x|* $rhs.x} $x {get y|* $rhs.y} $y {get z| * $rhs.z} $z |
    + $x $y $z
}

>> Vec 1 2 3 | dot Vec 4 5 6
32

>> def cross Vec (rhs:Vec) { let $v | let
    {\$v.y | * $rhs.z | - {\$v.z | * $rhs.y}} $x
    {\$v.z | * $rhs.x | - {\$v.x | * $rhs.z}} $y
    {\$v.x | * $rhs.y | - {\$v.y | * $rhs.x}} $z |
    Vec $x $y $z
}

>> Vec 5 10 3 | cross Vec 3 4 1
Vec (x = -2, y = 4, z = -10)
```
