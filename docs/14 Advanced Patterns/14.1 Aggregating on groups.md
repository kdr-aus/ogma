<iframe src="../.ibox.html?raw=true" style="border:none; position:fixed; width:40px; right:0; z-index=999;"></iframe>

# Aggregating on Groups

Working with aggregated data is common when transforming a dataset in preparation for charting.
A useful pattern is where a _series_ must be extracted from a flattened dataset, which forms the
_columns_, and the _rows_ are an aggregate of both the column filter and a row group filter.
For example, with the _diamonds.csv_ dataset, a chart could be constructed of the **price/carat**
per **clarity** class (on the x-axis) with **cut** class as the series.
To chart this, **cut** would define the columns, with **clarity** as the row entries.
An initial implementation using manual definitions of the columns would look like this:

```ogma
def price-per Table (cut:Str) {
    filter 'cut' --Str = $cut | fold {Tuple 0 0} {
        Tuple {+ #i.t0 $row.price} {+ #i.t1 $row.carat}
    } | let #i.t1 $carat | get t0 | / $carat
}

open 'diamonds.csv' | grp 'clarity'
| append --Fair { get value --Table | price-per 'Fair' }
| append --Good { get value --Table | price-per 'Good' }
| append --'Very Good' { get value --Table | price-per 'Very Good' }
| append --Ideal { get value --Table | price-per 'Ideal' }
| append --Premium { get value --Table | price-per 'Premium' }
| ren 'key' 'clarity'
| filter --cols != 'value'
```

The def `price-per` is used to filter the table by the _cut_ column and calculate the price/carat
metric. The _diamonds.csv_ table is opened up and grouped by _clarity_, which gives the **rows**.
A column is appended for _each_ cut class that is found in the table (`open diamonds.csv | grp cut`
can be used to quickly work out the distinct values). There is some tidy up at the end which names
the columns correctly and removes the 'value' column.

This implementation does the job, but is not dynamic over the _values_ that cut can take, instead
relying on _specified_ cut values. To be able to dynamically add the columns, a slightly different
approach is taken (`price-per` is used from above):

```ogma
open 'diamonds.csv' | let {grp 'clarity'} $g
| grp 'cut' | fold $g {
    let { \ $row | get key --Str } $cut
    | append --tmp { get value --Table | price-per $cut }
    | ren 'tmp' $cut
} | ren 'key' 'clarity'
| filter --cols != 'value'
```

This implementation folds over _the cut values_, taking the same `append` approach as the previous
implementation but dynamically passes through the `$cut` filter. The input into the fold is the
_grouped clarity table_, and since each append returns a new table, this becomes the input for the
next iteration. The appended column is given the name of the cut variable. This approach is
recommended to use when the values are unknown or subject to change, or too numerous to write out
manually.
